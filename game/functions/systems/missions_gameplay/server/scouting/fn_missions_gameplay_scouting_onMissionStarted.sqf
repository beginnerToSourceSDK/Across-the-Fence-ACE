/*
    File: fn_missions_gameplay_scouting_onMissionStarted.sqf
    Author: Savage Game Design
    Date: 2024-09-29
    Last Update: 2026-05-09
    Public: No

    Description:
        Handle mission start, updates hashmap values and creates scouting task.

    Parameter(s):
        _missionId - Id of the mission [NUMBER]

    Returns:
        Nothing

    Example(s):
        [_missionId] call vgm_s_fnc_missions_gameplay_scouting_onMissionStarted
 */

params ["_missionId"];

private _mission = [_missionId] call vgm_s_fnc_missions_getById;

if (isNil "_mission") exitWith {
    format ["Mission does not exist: %1", _missionId] call vgm_g_fnc_logError;
};

private _data = [_missionId, "scouting"] call vgm_s_fnc_missions_getSystemNetmap;

[_data, "guessedSitesMax", count ((_mission get "public" get "targetZone") call vgm_s_fnc_missions_zones_getSites)] call para_s_fnc_netmap_set;

// add task for the players
[_mission] spawn {
    params ["_mission"];
    sleep 10;

    private _playerGroup = _mission get "public" get "group";
    private _sites = +((_mission get "public" get "targetZone") call vgm_s_fnc_missions_zones_getSites);

    private _intelSitePositions = [];
    for "_" from 1 to 2 do {
        private _intelSite = selectRandom _sites;
        // Attempt to guarantee a minimum distance between sites
        for "_attempt" from 1 to 20 do {
            if (count _intelSitePositions > 0 && {(_intelSite get "pos") distance2D (_intelSitePositions select -1) < 300}) then {
                _intelSite = selectRandom _sites;
                continue;
            };
            break;
        };
        private _sitePosMarker = (_intelSite get "pos") getPos [50, random 360];
        _intelSitePositions pushBack _sitePosMarker;
        _sites = _sites - [_intelSite];
    };

    private _intelSitesStr = _intelSitePositions apply {
        format [
            "<execute expression='%2'>%1</execute>",
            (_x call BIS_fnc_posToGrid) joinString " ",
            format ["[[750,750], %1] call BIS_fnc_zoomOnArea", _x]
        ]
    } joinString "<br/>";

    private _parentTaskId = format ["vgm_scout_%1", _mission get "public" get "id"];

    [
        _playerGroup,
        _parentTaskId,
        [
            [
                "STR_VGM_MISSIONS_SCOUTING_TASK_DESCRIPTION",
                _intelSitesStr,
                format [
                    "<execute expression='[""vgm_missions"", ""scouting""] call vgm_c_fnc_openFieldManual'>%1</execute>",
                    localize "str_a3_rscdisplayinterrupt_buttontutorialhints"
                ]
            ],
            "STR_VGM_MISSIONS_SCOUTING_TASK_TITLE"
        ],
        objNull,
        "ASSIGNED",
        -1,
        true,
        "scout"
    ] call BIS_fnc_taskCreate;

    {
        [
            _playerGroup,
            [format ["%1-%2", _parentTaskId, _forEachIndex + 1], _parentTaskId],
            [
                ["STR_VGM_MISSIONS_SCOUTING_SUBTASK_DESCRIPTION"],
                ["STR_VGM_MISSIONS_SCOUTING_SUBTASK_TITLE"]
            ],
            _x vectorMultiply [1, 1, 0],
            ["ASSIGNED", "CREATED"] select (_forEachIndex isEqualTo 0),
            -1,
            false,
            "scout"
        ] call BIS_fnc_taskCreate;
    } forEach _intelSitePositions;
};
